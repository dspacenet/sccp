
---------------------------------------------------------
---- SCCP Virtual Machine
---- By: HÃ©ctor Delgado, Camilo Rueda and Frank Valencia.
---------------------------------------------------------


---- Module for String Pattern matching
mod STRING-PATTERN is
 pr STRING .
 sort Pattern .
 subsort String < Pattern .
 sort Comp .
 
 op car : Pattern -> Pattern .
 op cdr : Pattern -> Pattern . 
 op nil : -> Pattern .
 op _._ : Pattern Pattern -> Pattern [ctor assoc id: ""] . 
 op * : -> Pattern . 
 op ? : -> Pattern .
 op (_|_) : Pattern Pattern -> Comp [ctor] . 
 
 vars P P' : Pattern .
 var S : String .
 
 ---Defining functions car and cdr (inspired in lisp) but for strings.
 ceq car(S . P) = substr(S,0,1) if S =/= "" .
 ceq cdr(S . P) = substr(S,1,length(S)) . P if S =/= "" .
 eq car(* . P) = * .
 eq cdr(* . P) = P .
 eq car(? . P) = ? .
 eq cdr(? . P) = P .
 ---------------
 
 
 crl [test4]:
  ( P | P' )  =>  ( cdr(P) | cdr(P') )  
  if 
  car(P) == car(P') /\ 
  P =/= "" 
  /\ 
  P' =/= ""  .
  
 crl [test5]:
  ( P | * . P' )  =>  ( cdr(P) | * . P' ) 
  if
  P =/= ""
  .
  
 rl [test6]:
  ( P | * . P' )  =>  ( P |  P' ) .
  
 crl [test7]:
  ( P | ? . P' )  =>  ( cdr(P) | P' ) 
  if
 P =/= ""
  .
endm
-----------------

load gen-tree.maude

----Module that define the type of the constraints.
mod CONST is
 pr STRING .
 sort Const .
 pr STRING-PATTERN .
 subsort Pattern < Const . 
 
endm
----------------


----Definition of views for the constraints and the constraint sets.
view Const from TRIV to CONST is
  sort Elt to Const .
endv

view ConstSet from TRIV to SET{Const} * (sort NeSet{Const} to NeConstSet, sort Set{Const} to ConstSet) is
  sort Elt to ConstSet .
endv
----------------

----Module that implements the store as a gen-tree of sets of constraints.
mod CLOUD is

 pr BOOL .
 sort Cloud .
 pr INT .
 pr CONST .
 
 pr SET{Const} * (sort NeSet{Const} to NeConstSet, sort Set{Const} to ConstSet) .
 inc GEN-TREE{ConstSet} * (sort Tree{ConstSet} to TreeConstSet, sort Forest{ConstSet} to ForestConstSet) .
 
 subsort TreeConstSet < Cloud .
 
 ---Main operations
 op (_)^_ : Cloud Nat -> Cloud [ctor] . 
 op _U[_]_ : TreeConstSet TreeConstSet Nat -> TreeConstSet .
 ------
 
 ---Auxiliar operations
 op replace : ForestConstSet TreeConstSet Nat -> ForestConstSet .
 op find : ForestConstSet Nat -> TreeConstSet . 
 op auxfind : TreeConstSet Nat -> TreeConstSet .
 op insertel : String Cloud -> Cloud .
 op withoutlast : ForestConstSet -> ForestConstSet .
 op lastelement : ForestConstSet -> TreeConstSet .
 op mergeforest : ForestConstSet ForestConstSet -> ForestConstSet .
 op expandforest : ForestConstSet ForestConstSet Nat -> ForestConstSet .
 op howmanychildren : Int Int -> Int .
 op auxreplace : TreeConstSet TreeConstSet Int -> ForestConstSet .
 ---
 
 vars tc t : TreeConstSet .
 vars T T1 OF NF : ForestConstSet .
 vars N NC : Const .
 var NS : ConstSet .
 var C : Cloud .
 vars N1 NC1 : Int .
 var i : Nat . 

 ---Auxiliar functions
 eq withoutlast(t : empty-forest) = empty-forest .
 eq withoutlast(t : T) = t : withoutlast(T) .
 eq lastelement(t : empty-forest) = t .
 eq lastelement(t : T) = lastelement(T) . 
 eq mergeforest(empty-forest,NF) = NF .
 eq mergeforest(OF,NF) = mergeforest(withoutlast(OF),lastelement(OF) : NF) .
 eq expandforest(OF,NF,0) = mergeforest(OF,NF) .
 eq expandforest(OF,NF,s(N1)) = expandforest(OF,empty[empty-forest] : NF,N1) .
 ceq howmanychildren(N1,NC1) = 0 if NC1 > N1 . 
 eq howmanychildren(N1,NC1) = (N1 - NC1) + 1 .
 eq auxreplace(tc,t,N1) = replace(expandforest(children(tc),empty-forest,howmanychildren(N1,#children(tc))),t,N1) .
 eq find(t : T, 0) = t .
 eq find(t : T, s(N1)) = find(T,N1) .
 eq auxfind(t,N1) = find(expandforest(children(t),empty-forest,howmanychildren(N1,#children(t))),N1) .
 eq replace(tc : T,t,0) = t : T .
 eq replace(tc : T,t,s(N1)) = tc : replace(T,t,N1) .
 ---
 
 eq (tc) ^ N1 = auxfind(tc,N1) .  
 eq tc U [t] N1 = root(tc)[auxreplace(tc,t,N1)] . 
  
 eq insertel(N,C) = (insert(N,root(C)))[children(C)] .     
 
endm
---------------------


---Defining the spatial instructions
mod SPAINSTRUC is
 pr META-LEVEL .
 sort SpaInstruc .
 pr QID .
 pr CONST .
 
 vars P1 P2 P3 : SpaInstruc .
 var i : Nat .
 vars X X1 : Qid .
 var c : Const .
 
 ---Defining the structure of the spatial instructions
 op tell(_) : Const -> SpaInstruc [ctor] .
 op skip : -> SpaInstruc [ctor] .
 op s : SpaInstruc Nat -> SpaInstruc [ctor] .
 op _||_ : SpaInstruc SpaInstruc -> SpaInstruc [ctor comm assoc prec 50] .
 op rec _in_ : Qid SpaInstruc -> SpaInstruc [ctor prec 30] .
 op when_do_ : Const SpaInstruc -> SpaInstruc [ctor prec 30] .
 op v(_) : Qid -> SpaInstruc [ctor] .
 op x : SpaInstruc Nat -> SpaInstruc [ctor] .
 op unless_next_ : Const SpaInstruc -> SpaInstruc [ctor prec 30] .
 op next_ : SpaInstruc -> SpaInstruc [ctor prec 30] . 
  
 op replaceVar : SpaInstruc Qid SpaInstruc -> SpaInstruc . 
 
 ---Derived Operators
 op enter_do_ : Nat SpaInstruc -> SpaInstruc [ctor prec 30] .
 op exit_do_ : Nat SpaInstruc -> SpaInstruc [ctor prec 30] .
 op _@_ : SpaInstruc Nat -> SpaInstruc [ctor prec 30] .
 op _^_ : SpaInstruc Nat -> SpaInstruc [ctor prec 30] .
 
 ----
 
 ---Defining the replaceVar funtion for each spatial instruction.
 eq replaceVar(P1 || P2,X,P3) = replaceVar(P1,X,P3) || replaceVar(P2,X,P3) .
 eq replaceVar(when c do P1,X,P3) = when c do replaceVar(P1,X,P3) .
 eq replaceVar(tell(c),X,P3) = tell(c) .
 eq replaceVar(skip,X,P3) = skip .
 eq replaceVar(v(X), X ,P3) = P3 .
 ceq replaceVar(v(X), X1 ,P3) = v(X) if X =/= X1 .
 eq replaceVar(s(P1,i), X , P3) = s(replaceVar(P1,X,P3),i) .
 eq replaceVar(x(P1,i),X,P2) = x(replaceVar(P1,X,P2),i) .
 eq replaceVar(rec X in P1, X, P2) = rec X in P1 . 
 ceq replaceVar(rec X1 in P1, X, P2) = rec X1 in replaceVar(P1, X , P2) if X1 =/= X  .
 eq replaceVar(unless c next P1,X,P3) = unless c next replaceVar(P1,X,P3) .
 eq replaceVar(next P1,X,P3) = next replaceVar(P1,X,P3) .
 ---
 
 --- Equations for deleting skip instructions and applying the extrusion operator.
 
 eq s(x(P1,i) || P2,i) = 
 P1 || s(P2,i) .
 
  eq s(x(P1,i),i) =
 P1 .
 
  eq skip || P1
  = P1 .
  
 eq s(skip,i) 
  = skip .
  
 eq x(skip,i)
  = skip .
 ------
endm
------------

---Defining the main configuration for a SCCP system.
mod SCCP-SYMBOLS is
 pr CLOUD .
 pr SPAINSTRUC .
 sort Conf .

 op <_;_> : SpaInstruc Cloud -> Conf [ctor] .
 
endm 
--------


---Module that defines the operational semantics of the language.
mod SCCP is
 inc SCCP-SYMBOLS .
 pr STRING-PATTERN .
 pr CONST .
 
 var C1 : ConstSet .
 var S1 : Const .
 
 var C : Const .
 vars CL CL' TM TM' : Cloud .
 vars P Q P' : SpaInstruc .
 var SP : SpaInstruc .
 var i : Nat .
 var F : ForestConstSet .
 var X : Qid .
 
 ---Rules for Tell Operator
 rl < tell(C) ; CL > =>
 < skip ; insertel(C,CL) > .
 
 rl < tell(C) || Q ; CL > =>
 < skip || Q ; insertel(C,CL) > .
 ------
 
 ---Rules for When Do Operator 
 crl < when C do P ; (S1 , C1)[F] > =>
 < P ; (S1 , C1)[F] > 
 if 
 (S1 | C) => ("" | "") .
 
 crl < Q || when C do P ; (S1 , C1)[F] > => 
 < Q || P ; (S1 , C1)[F] > 
 if 
 (S1 | C) => ("" | "") .
 ------
 
 ---Rules for Unless C do P 
  crl [Unless1] : 
  < unless C next P ; (S1 , C1)[F] > =>
 < skip ; (S1 , C1)[F] > 
 if 
 (S1 | C) => ("" | "") .
 
  crl [Unless2] : 
  < Q || unless C next P ; (S1 , C1)[F] > =>
 < Q || skip ; (S1 , C1)[F] > 
 if 
 (S1 | C) => ("" | "") .
 ---
 
 ---Rules for Spatial instructions
 crl [SpaInstruc] :
 < s(P,i) ; CL > => 
 < s(P',i) ; CL U [CL'] i > 
 if 
 < P ; (CL) ^ i > => 
 < P' ; CL' > /\ P =/= P' .

 crl [SpaInstruc2] :
 < Q || s(P,i) ; CL > => 
 < Q || s(P',i) ; CL U [CL'] i > 
 if 
 < P ; (CL) ^ i > => 
 < P' ; CL' > /\ P =/= P' .
 ------
 
 ---Rules for Recursion instructions
 crl [Rec] : 
 < rec X in P ; CL > => 
 < P' ; CL' > 
 if < replaceVar(P,X,rec X in P) ; CL > => 
 < P' ; CL' > .
 
  crl [Rec2] : 
 < rec X in P || Q ; CL > => 
 < P' || Q ; CL' > 
 if < replaceVar(P,X,rec X in P) ; CL > => 
 < P' ; CL' > .
 ------
 
endm
--------

mod SYNTAXMOD is
 inc SCCP .
 
 op [_]_ : SpaInstruc Nat -> SpaInstruc .
 op xtr : SpaInstruc Nat -> SpaInstruc . 
 op repeat_ : SpaInstruc -> SpaInstruc [prec 30] .
 op whenever_do_ : Const SpaInstruc -> SpaInstruc [prec 30] .
 op until_do_ : Const SpaInstruc -> SpaInstruc [prec 30] .
 op once_do_ : Const SpaInstruc -> SpaInstruc [prec 30] .
 op while_do_ : Const SpaInstruc -> SpaInstruc [prec 30] .

 vars P P' : SpaInstruc .
 var i : Nat .
 var c : Const .
 
  
 eq [P]i = s(P,i) .
 eq xtr(P,i) = x(P,i) .
 eq enter i do P = s(P,i) .
 eq exit i do P = x(P,i) .
 eq P @ i = s(P,i) . 
 eq P ^ i = x(P,i ) .
 eq repeat P = rec 'vrep in ( P || next v('vrep)) .
 eq whenever c do P = rec 'vwhen in ( when c do P || next v('vwhen)) .
 eq until c do P = rec 'vuntil in (P || unless c next v('vuntil)) .
 eq once c do P = rec 'vonce in (when c do P || unless c next v('vonce)) . 
 eq while c do P = rec 'vwhile in (when c do P || when c do next v('vwhile)) .  
endm

mod NTCC is
 inc SCCP-SYMBOLS .

 
 var C : Const .
 var c : Const .
 var X : Qid .
 vars P Q P' : SpaInstruc .
 var i : Nat .
 op f : SpaInstruc -> SpaInstruc .
 
 eq f(next P) = P .
 eq f(P || Q) = f(P) || f(Q) .
 eq f(tell(c)) = skip .
 eq f(when c do P) = skip .
 eq f(unless c next P) = P .
 eq f(rec X in P) = rec X in f(P) . 
 eq f(s(P,i)) = s(f(P),i) .
 eq f(x(P,i)) = x(f(P),i) .
 eq f(skip) = skip .
 

endm


mod SCCP-RUN is
  pr SYNTAXMOD .
  
endm

mod NTCC-RUN is
 pr NTCC .

endm


